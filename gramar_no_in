%start program
%%
program          : linebreak complete_commands linebreak
                 | linebreak
                 ;
complete_commands: complete_commands newline_list complete_command
                 |                                complete_command
                 ;
complete_command : list separator_op
                 | list
                 ;
list             : list separator_op and_or
                 |                   and_or
                 ;
and_or           :                         pipeline
                 | and_or TK_AND_IF linebreak pipeline
                 | and_or TK_OR_IF  linebreak pipeline
                 ;
pipeline         :      pipe_sequence
                 | TK_BANG pipe_sequence
                 ;
pipe_sequence    :                             command
                 | pipe_sequence TK_PIPE linebreak command
                 ;
command          : simple_command
                 | compound_command
                 | compound_command redirect_list
                 | function_definition
                 ;
compound_command : brace_group
                 | subshell
                 | for_clause
                 | case_clause
                 | if_clause
                 | while_clause
                 | until_clause
                 ;
subshell         : TK_LPAREN compound_list TK_RPAREN
                 ;
compound_list    : linebreak term
                 | linebreak term separator
                 ;
term             : term separator and_or
                 |                and_or
                 ;
for_clause       : TK_FOR name                                      do_group
                 | TK_FOR name                       sequential_sep do_group
                 | TK_FOR name linebreak in          sequential_sep do_group
                 | TK_FOR name linebreak in wordlist sequential_sep do_group
                 ;
name             : TK_NAME                     /* Apply rule 5 */
                 ;                
in               : TK_IN
                 ;
wordlist         : wordlist TK_WORD
                 |          TK_WORD
                 ;
case_clause      : TK_CASE TK_WORD linebreak in linebreak case_list    TK_ESAC
                 | TK_CASE TK_WORD linebreak in linebreak case_list_ns TK_ESAC
                 | TK_CASE TK_WORD linebreak in linebreak              TK_ESAC
                 ;
case_list_ns     : case_list case_item_ns
                 |           case_item_ns
                 ;
case_list        : case_list case_item
                 |           case_item
                 ;
case_item_ns     :     pattern TK_RPAREN linebreak
                 |     pattern TK_RPAREN compound_list
                 | TK_LPAREN pattern TK_RPAREN linebreak
                 | TK_LPAREN pattern TK_RPAREN compound_list
                 ;
case_item        :     pattern TK_RPAREN linebreak     TK_DSEMI linebreak
                 |     pattern TK_RPAREN compound_list TK_DSEMI linebreak
                 | TK_LPAREN pattern TK_RPAREN linebreak     TK_DSEMI linebreak
                 | TK_LPAREN pattern TK_RPAREN compound_list TK_DSEMI linebreak
                 ;
pattern          :             TK_WORD         /* Apply rule 4 */
                 | pattern TK_PIPE TK_WORD         /* Do not apply rule 4 */
                 ;
if_clause        : TK_IF compound_list TK_THEN compound_list else_part TK_FI
                 | TK_IF compound_list TK_THEN compound_list           TK_FI
                 ;
else_part        : TK_ELIF compound_list TK_THEN compound_list
                 | TK_ELIF compound_list TK_THEN compound_list else_part
                 | TK_ELSE compound_list
                 ;
while_clause     : TK_WHILE compound_list do_group
                 ;
until_clause     : TK_UNTIL compound_list do_group
                 ;
function_definition : fname TK_LPAREN TK_RPAREN linebreak function_body
                 ;
function_body    : compound_command                /* Apply rule 9 */
                 | compound_command redirect_list  /* Apply rule 9 */
                 ;
fname            : TK_NAME                            /* Apply rule 8 */
                 ;
brace_group      : TK_LBRACE compound_list TK_RBRACE
                 ;
do_group         : Do compound_list Done           /* Apply rule 6 */
                 ;
simple_command   : cmd_prefix cmd_word cmd_suffix
                 | cmd_prefix cmd_word
                 | cmd_prefix
                 | cmd_name cmd_suffix
                 | cmd_name
                 ;
cmd_name         : TK_WORD                   /* Apply rule 7a */
                 ;
cmd_word         : TK_WORD                   /* Apply rule 7b */
                 ;
cmd_prefix       :            io_redirect
                 | cmd_prefix io_redirect
                 |            TK_ASSIGNMENT_WORD
                 | cmd_prefix TK_ASSIGNMENT_WORD
                 ;
cmd_suffix       :            io_redirect
                 | cmd_suffix io_redirect
                 |            TK_WORD
                 | cmd_suffix TK_WORD
                 ;
redirect_list    :               io_redirect
                 | redirect_list io_redirect
                 ;
io_redirect      :           io_file
                 | TK_IO_NUMBER io_file
                 |           io_here
                 | TK_IO_NUMBER io_here
                 ;
io_file          : TK_LESS       filename
                 | TK_LESSAND   filename
                 | TK_GREAT       filename
                 | TK_GREATAND  filename
                 | TK_DGREAT    filename
                 | TK_LESSGREAT filename
                 | TK_CLOBBER   filename
                 ;
filename         : TK_WORD                      /* Apply rule 2 */
                 ;
io_here          : TK_DLESS     here_end
                 | TK_DLESSDASH here_end
                 ;
here_end         : TK_WORD                      /* Apply rule 3 */
                 ;
newline_list     :              TK_NEWLINE
                 | newline_list TK_NEWLINE
                 ;
linebreak        : newline_list
                 | /* empty */
                 ;
separator_op     : TK_AND
                 | TK_SEMI
                 ;
separator        : separator_op linebreak
                 | newline_list
                 ;
sequential_sep   : TK_SEMI linebreak
                 | newline_list
                 ;
